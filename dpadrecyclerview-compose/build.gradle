plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
}

apply from: "$rootProject.projectDir/gradle/publish.gradle"
apply from: "$rootProject.projectDir/gradle/coverage.gradle"

android {
    namespace 'com.rubensousa.dpadrecyclerview.compose'
    compileSdk versions.compileSdkVersion

    defaultConfig {
        minSdk versions.minSdkVersion
        targetSdk versions.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments useTestStorageService: 'true'
        testInstrumentationRunnerArguments listener: 'com.rubensousa.dpadrecyclerview.testfixtures.recording.TestRecordingListener'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
        }
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.androidx.compose.compiler.get()
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

}

dependencies {
    api project(':dpadrecyclerview')
    implementation libs.androidx.appcompat
    implementation libs.androidx.recyclerview
    implementation libs.androidx.customview.poolingcontainer
    implementation libs.androidx.compose.ui
    implementation libs.listenablefuture

    // Test dependencies
    debugImplementation libs.androidx.test.compose.ui.manifest
    debugImplementation libs.androidx.compose.ui.tooling
    debugImplementation libs.androidx.compose.ui.tooling.preview
    debugImplementation libs.androidx.compose.material3
    debugImplementation libs.androidx.customview

    androidTestImplementation project (':dpadrecyclerview-testing')
    androidTestImplementation project (':dpadrecyclerview-test-fixtures')
    androidTestImplementation libs.androidx.test.compose.ui.junit4
    androidTestUtil libs.androidx.test.services
}
